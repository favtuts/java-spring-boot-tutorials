// At build.gradle root, we config for sub-module to have the shared configuration, inherit the plugin and dependencies of the parent.

// Then after that these sub-modules no need to configure any more.

allprojects {
	group 'com.favtuts'
	version '1.0-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'idea'

	repositories {
		mavenCentral()
		jcenter()
	}
}

buildscript {
	ext {
		springBootVersion = '2.7.15'
		springManagementVersion = '1.0.15.RELEASE'
		lombokVersion = '1.18.28'

		guavaVersion = '32.1.2-jre'
		commonLang3Version = '3.13.0'
		mysqlVersion = '8.0.13'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:${springManagementVersion}")
	}
}

subprojects {
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'

	sourceCompatibility = 11
	targetCompatibility = 11

	def defaultEncoding = 'UTF-8'
	tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

	javadoc {
		options.encoding = defaultEncoding
		options.addBooleanOption('Xdoclint:none', true)
	}

	compileJava.dependsOn(processResources)

	springBoot {
		buildInfo()
	}

	dependencies{
		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
		annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
		compileOnly "org.springframework.boot:spring-boot-configuration-processor"
		compileOnly "org.projectlombok:lombok:${lombokVersion}"

		// SPRING DEPENDENCIES
		implementation "org.springframework.boot:spring-boot-starter-data-jpa"
		implementation "mysql:mysql-connector-java:${mysqlVersion}"

		// Utilities
		implementation "com.google.guava:guava:${guavaVersion}"
		implementation "org.apache.commons:commons-lang3:${commonLang3Version}"

		// TEST
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

}

project(':service') {
	dependencies {
		implementation project(':common')
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-devtools'
	}
}

project(':common'){

}